plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
}

group = 'com.yesjm'
version = '0.0.1'
description = 'A lightweight Slack message sender library'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jacoco {
    toolVersion = "0.8.12"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

dependencies {
    testImplementation("org.mockito:mockito-core:5.3.1")
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn(test)
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.6 // 최소 60% 커버리지 설정
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}